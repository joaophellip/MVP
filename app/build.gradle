// Apply plug-ins using Gradle DSL provider
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}


android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.cozo.cozomvp"
        minSdkVersion 22
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions{
        abortOnError false
    }
    testOptions{
        unitTests{
            includeAndroidResources = true //Check: http://robolectric.org/getting-started/
        }
    }
}

// Dependencies for external libraries
dependencies {

    //Play-services-basement
    //implementation 'com.google.android.gms:play-services-basement:15.0.1'

    // Kotlin Standard Library JDK 7
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Anko is a kotlin lib to easy Android app development. License: Apache 2.0
    implementation "org.jetbrains.anko:anko:$anko_version"
    implementation "org.jetbrains.anko:anko-design:$anko_version"

    // Firebase libraries
    //implementation 'com.google.firebase:firebase-core:15.0.1'
    implementation('com.google.firebase:firebase-auth:16.0.5') {
        exclude group: "com.android.support"
    }
    implementation 'com.firebaseui:firebase-ui-auth:4.2.1'
    implementation 'com.googlecode.libphonenumber:libphonenumber:7.6.1'
    implementation('com.google.android.gms:play-services-auth:16.0.1')

    // Mosby is a Model-View-Presenter framework for Android apps. License: Apache 2.0
    implementation 'com.hannesdorfmann.mosby3:mvp:3.1.0'
    implementation 'com.hannesdorfmann.mosby3:mvp-lce:3.1.0'

    // ButterKnife is an Annotation library. License: Apache 2.0
    implementation 'com.jakewharton:butterknife:8.8.1'
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'

    // Retrofit is a library that serves as a REST client. It sits on top of OkHttp. License: Apache 2.0
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // OkHttp is a HTTP client for Android and Java applications. Apache 2.0
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    // SocketIO is a client library that is ported from the Socket IO JavaScript client library. License MIT
    implementation('io.socket:socket.io-client:1.0.0') {
        // excluding org.json which is already provided by Android
        exclude group: 'org.json', module: 'json'
    }

    // ReactiveX is a library for composing asynchronous and event-based programs. License: Apache 2.0
    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    // Android Support libraries which are required for creating customized layouts
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'

    // CircleImageView is a support library for creating circular images. License: Apache 2.0
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    // Google Play services libraries
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation('com.google.android.gms:play-services-maps:16.0.0') {
        exclude group: "com.android.support"
    }

    // Handy extensions to the Google Maps Android API.
    implementation 'com.google.maps.android:android-maps-utils:0.5'

    // Dependency for masking credit card data
    implementation "com.github.santalu:mask-edittext:1.0.2"

    // Add local JAR files to project if any
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Card Form is a ready made card form layout to be shown in Payment activity. License: MIT
    implementation 'com.braintreepayments:card-form:3.3.0'

    // JUnit 4, Mockito and Robolectric frameworks for Unit testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation "org.robolectric:robolectric:3.8"

    // Espresso Framework for Instrumented testing
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'
}

// Add dependencies for basic libs for the google api services. Can't be added with other plug-ins
// in order to avoid dependency collisions are introduced
apply plugin: 'com.google.gms.google-services'
